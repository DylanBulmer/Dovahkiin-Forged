plugins {
    id 'fabric-loom' version '1.8-SNAPSHOT'
    id 'maven-publish'
}

apply from: "$projectDir/gradle/dependencies.gradle"

version = project.mod_version
group = 'net.dovahkiinforged'

base {
    archivesName = project.archives_base_name
}

repositories {
    mavenLocal()
    maven {
        url = uri("https://maven.pkg.github.com/DylanBulmer/dovahkiin-forged-commons")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
            password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

fabricApi {
    configureDataGeneration()
}

loom {
    splitEnvironmentSourceSets()

    mods {
        "dovahkiinforged" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }

}

dependencies {
    // To change the versions see /gradle/dependencies.gradle
    minecraft libs.minecraft
    mappings libs.yarn
    modImplementation libs.fabric

    implementation libs.commons
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}"}
    }
}

/**
 * This task is used to copy textures, lang files, and other resources from the commons library.
 */
tasks.register('copyCommonsResources', Copy) {
    def dependencyJar = configurations.runtimeClasspath.find {
        it.name.contains('dovahkiin-forged-commons')
    }
    if (dependencyJar) {
        from zipTree(dependencyJar)
    }

    // Include the assets folder from the commons dependency
    include 'assets/**'

    // Copy to into the build resources directory
    into layout.buildDirectory.dir("resources/main").get().asFile
}

// telling the build process that the project relies on this task.
processResources.dependsOn 'copyCommonsResources'
